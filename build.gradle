plugins {
    id 'idea'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'io.lindstrom'
version = '0.30'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

ext {
    ideaGeneratedSources = file('build/generated/sources/annotationProcessor/java')
}

idea.module {
    sourceDirs += ideaGeneratedSources
    generatedSourceDirs += ideaGeneratedSources
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.immutables:value:2.10.1'
    annotationProcessor "org.immutables:value:2.5.6"
    testImplementation('junit:junit:4.12')
}

test {
    //useJUnitPlatform()
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

publishing {
    repositories {
        maven {
            def releaseRepo = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://oss.sonatype.org/content/repositories/snapshots/"

            name = "OSSRH"
            url = isReleaseVersion ? releaseRepo : snapshotRepo

            credentials {
                username = findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                password = findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            artifactId = 'm3u8-parser'

            from components.java

            pom {
                groupId = 'io.lindstrom'
                name = 'm3u8-parser'
                description = 'm3u8 parser'
                url = 'https://github.com/carlanton/m3u8-parser'
                packaging = 'jar'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/carlanton/m3u8-parser/blob/master/LICENSE'
                    }
                }

                scm {
                    url = 'https://github.com/carlanton/m3u8-parser.git'
                    connection = 'scm:git:https://github.com/carlanton/m3u8-parser.git'
                    developerConnection = 'scm:git:git@github.com:carlanton/m3u8-parser.git'
                }

                developers {
                    developer {
                        id = 'antonlindstrom'
                        name = 'Anton Lindstr√∂m'
                        email = 'carlantonlindstrom@gmail.com'
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
