group 'io.lindstrom'
version '0.18'

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

ext {
    ideaGeneratedSources = file('build/generated/sources/annotationProcessor/java')
}

idea.module {
    sourceDirs += ideaGeneratedSources
    generatedSourceDirs += ideaGeneratedSources
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.immutables:value:2.5.6'
    annotationProcessor "org.immutables:value:2.5.6"
    testCompile 'junit:junit:4.12'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

archivesBaseName = "m3u8-parser"

if (project.hasProperty('ossrhUsername')) {
    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'm3u8-parser'
                    packaging 'jar'
                    artifactId 'm3u8-parser'
                    description 'm3u8 parser'
                    url 'https://github.com/carlanton/m3u8-parser'

                    scm {
                        connection 'scm:git:https://github.com/carlanton/m3u8-parser.git'
                        developerConnection 'scm:git:git@github.com:carlanton/m3u8-parser.git'
                        url 'https://github.com/carlanton/m3u8-parser.git'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'https://github.com/carlanton/m3u8-parser/blob/master/LICENSE'
                        }
                    }

                    developers {
                        developer {
                            id 'antonlindstrom'
                            name 'Anton Lindstr√∂m'
                            email 'carlantonlindstrom@gmail.com'
                        }
                    }
                }
            }
        }
    }
}
